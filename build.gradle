plugins {
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
}

group = 'com.agileactors'
version project.hasProperty('version') ? version : '1.0.0'
sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    annotationProcessor(platform("org.springframework.boot:spring-boot-dependencies:2.7.3"))
    api(platform("org.springframework.boot:spring-boot-dependencies:2.7.3"))

    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-actuator'
    api 'org.springframework.boot:spring-boot-starter-validation'
    api 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'com.fasterxml.jackson.core:jackson-databind'
    runtimeOnly 'org.hibernate:hibernate-ehcache'
    runtimeOnly 'org.flywaydb:flyway-core'
    runtimeOnly 'com.h2database:h2'

}

tasks.named('test') {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes["Implementation-Version"] = "${archiveVersion.get()}"
        attributes["Implementation-Title"] = "Spring Boot CRUD API"
        attributes["Implementation-Vendor"] = "Agile Actors"
        attributes["Created-By"] = System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            from project.components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/agileactors/agile-actors-spring-boot-crud-api"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("PACKAGES_TOKEN")
            }
        }
    }
}

task printVersion {
    doLast {
        println project.version
    }
}